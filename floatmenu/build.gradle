apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def buildInfo = [
    siteUrl: 'https://gurunars.github.io/android-crud-ui',
    gitUrl: 'git@github.com:gurunars/android-crud-ui.git',
    name: "floatmenu",
    group: "com.gurunars.floatmenu",
    version: "0.40",
    versionCode: 40,
    license: 'Apache-2.0'
]

group = buildInfo.group
version = buildInfo.version

android {
    compileSdkVersion 25
    buildToolsVersion '23.0.3'

    lintOptions.abortOnError false

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode buildInfo.versionCode
        versionName buildInfo.version
    }

    return void
}

dependencies {
    compile 'com.android.support:support-annotations:25.1.1'
    compile 'com.android.support:support-compat:25.1.1'
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
    compile project(':utils')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayApikey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = buildInfo.name
        websiteUrl = buildInfo.siteUrl
        vcsUrl = buildInfo.gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

bintrayUpload.dependsOn([build, sourcesJar])

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
