apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def buildInfo = [
        siteUrl: 'https://github.com/gurunars/leaflet-view',
        gitUrl: 'https://github.com/gurunars/leaflet-view.git',
        name: "leaflet-view",
        group: "com.gurunars.leaflet_view",
        version: "0.12",
        versionCode: 12
]

group = buildInfo.group
version = buildInfo.version

android {
    compileSdkVersion 25
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode buildInfo.versionCode
        versionName buildInfo.version
    }

    android.lintOptions.abortOnError false

    return void
}

dependencies {
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
    compile 'com.esotericsoftware:kryo:4.0.0'
    compile 'javax.inject:javax.inject:1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.1'
    compile 'com.android.support:support-annotations:25.1.1'
    compile 'com.android.support:support-v13:25.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayApikey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = buildInfo.name
        websiteUrl = buildInfo.siteUrl
        vcsUrl = buildInfo.gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

bintrayUpload.dependsOn([build, sourcesJar])

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
